/*
 * Project: LibUtils
 * Copyright (C) 2013 alf.labs gmail com,
 *
 *  This program is free software: you can redistribute it and/or modify
 *  it under the terms of the GNU General Public License as published by
 *  the Free Software Foundation, either version 3 of the License, or
 *  (at your option) any later version.
 *
 *  This program is distributed in the hope that it will be useful,
 *  but WITHOUT ANY WARRANTY; without even the implied warranty of
 *  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *  GNU General Public License for more details.
 *
 *  You should have received a copy of the GNU General Public License
 *  along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

apply plugin: "com.android.library"

if (!gradle.gradleVersion.startsWith("4")) {
    // jackOptions { enabled true } is not supported in library projects yet.
    // From http://stackoverflow.com/questions/3797512 :
    // Java8 not fully supported in library projects yet, https://code.google.com/p/android/issues/detail?id=211386
    // this is a temporary workaround to get at least lambdas compiling.
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xbootclasspath/a:" + System.properties.get("java.home") + "/lib/rt.jar"
        }
    }
}

android {
    compileSdkVersion 26

    if (!gradle.gradleVersion.startsWith("4")) {
        buildToolsVersion "25.0.2"
    } else {
        buildToolsVersion "26.0.0"
    }

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }

    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main {
            java {
                exclude "java/**"
            }
        }
    }

    lintOptions {
        abortOnError false
    }

    dexOptions {
        incremental true
    }
}

dependencies {
    compile project(":LibUtilsJava")

    // Used by InjectionValidator
    compile "com.google.truth:truth:0.34"
    // Used for optional annotation.RequiresApi
    compile "com.android.support:appcompat-v7:24.2.1"

    // Note: annotationProcessor starting with gradle 2.2 replaces the previous APT (annotation processor plugin)
    compile "com.google.dagger:dagger:2.5"
    annotationProcessor "com.google.dagger:dagger-compiler:2.5"

    // Testing: Robolectric, Mockito and Truth
    testCompile "org.mockito:mockito-core:2.2.9"
    testCompile "org.robolectric:robolectric:3.1" // uses hamcrest 1.3
    testCompile "com.google.truth:truth:0.34"     // to match hamcrest 1.3 from Robolectric
    testCompile "junit:junit:4.12"                // to match hamcrest 1.3 from Robolectric
}
